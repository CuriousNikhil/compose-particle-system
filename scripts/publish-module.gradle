apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {

        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {

        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}
artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {

                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    artifact("$buildDir/libs/${project.getName()}-${version}.jar")
                }

                artifact androidSourcesJar
//                artifact javadocJar

                pom {
                    name = 'quarks'
                    description = 'A light weight particle system for Jetpack Compose'
                    url = 'https://github.com/CuriousNikhil/compose-particle-system'
                    licenses {
                        license {
                            name = 'Apache License, Version 2.0'
                            url = 'https://github.com/CuriousNikhil/compose-particle-system/blob/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'curiousnikhil'
                            name = 'Nikhil Chaudhari'
                            email = 'nikhyl777@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/CuriousNikhil/compose-particle-system.git'
                        developerConnection = 'scm:git:ssh://github.com/CuriousNikhil/compose-particle-system.git'
                        url = 'https://github.com/CuriousNikhil/compose-particle-system'
                    }
                }
            }
        }
    }
}

ext["signing.keyId"] = rootProject.ext["signing.keyId"]
ext["signing.password"] = rootProject.ext["signing.password"]
ext["signing.secretKeyRingFile"] = rootProject.ext["signing.secretKeyRingFile"]

signing {
    sign publishing.publications
}
